// groovylint-disable NestedBlockDepth
plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '8.2.1'
}

group = 'io.github.bzkf'
version = '0.1.2' // x-release-please-version
description = 'Zenzy to FHIR'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', '2025.1.1')
    set('hapiVersion', '8.8.0')
}

dependencies {
    implementation project(':lib')
    implementation 'org.springframework.cloud:spring-cloud-stream'
    implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'net.logstash.logback:logstash-logback-encoder:9.0'
    implementation 'org.miracum:kafka-fhir-serializer:3.0.0'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'

    testImplementation "ca.uhn.hapi.fhir:hapi-fhir-validation:${hapiVersion}"
    testImplementation "ca.uhn.hapi.fhir:hapi-fhir-validation-resources-r4:${hapiVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.cloud:spring-cloud-stream-test-support'
    testImplementation 'org.apache.kafka:kafka-streams-test-utils'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

spotless {
    java {
        target('**/*.java')
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
    }
}

// via <https://stackoverflow.com/questions/24936781/gradle-plugin-project-version-number>
task printVersion {
    doLast {
        println project.version
    }
}
